buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.intermine', name: 'plugin', version: System.getProperty("imVersion")
    }
}
apply plugin: 'dbmodel'
apply plugin: 'biocore-dbmodel'

sourceSets {
    main {
        java {
            srcDirs = ['build/gen']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

configurations {
    bioCoreModelArtifact
}

dependencies {
    compile project(':bio-core')
    compile group: "org.intermine", name: "intermine-objectstore", version: System.getProperty("imVersion")
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'commons-lang:commons-lang:2.6'
}

dbModelConfig {
    objectStoreName = "os.bio-test"
    modelName = "genomic"
    defaultInterminePropertiesFile = "default.intermine.bio-test.properties"
}

task copyMineProperties(type: Copy) {
        from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
        into sourceSets.main.output.resourcesDir
        rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }
        inputs.sourceFiles.stopExecutionIfEmpty()
}

task copyGenomicModel(type: Copy, overwrite: true) {
    from "../core/core.xml"
    into sourceSets.main.output.resourcesDir

    rename { fileName -> fileName.replace('core.xml', 'genomic_model.xml') }
}

task buildBioCoreModelJar(type: Jar) {
    from(sourceSets.main.output)
}

tasks.buildDB.dependsOn ':bio-testresources:createUnitTestDatabases'
tasks.install.dependsOn 'buildBioCoreModelJar'

artifacts {
    bioCoreModelArtifact buildBioCoreModelJar
}
