buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath group: 'org.intermine', name: 'plugin', version: '1.+'
    }
}
apply plugin: 'database'
apply plugin: 'biocoredb'

sourceSets {
    main {
        java {
            srcDirs = ['build/gen']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

configurations {
    bioCoreModelArtifact
}

dependencies {
    compile group: "org.intermine", name: "intermine-objectstore", version: imVersion
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'commons-lang:commons-lang:2.6'
}

dbConfig {
    objectStoreName = "os.bio-test"
    modelName = "genomic"
    defaultInterminePropertiesFile = "default.intermine.bio-test.properties"
    soTermListFilePath = "model/src/main/resources/so_term_list.txt"
    soAdditionFilePath = "model/build/so_additions.xml"
}

task copyMineProperties(type: Copy) {
    from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
    into sourceSets.main.output.resourcesDir

    rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }

    inputs.sourceFiles.stopExecutionIfEmpty()
}

task buildBioCoreModelJar(type: Jar) {
    from(sourceSets.main.output)
}

tasks.install.dependsOn 'buildBioCoreModelJar'

artifacts {
    bioCoreModelArtifact buildBioCoreModelJar
}
