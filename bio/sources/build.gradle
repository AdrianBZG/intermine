ext {
    imVersion = "2.0.0-SNAPSHOT"
    bioVersion = "2.0.0-SNAPSHOT"
}

    buildscript {
        repositories {
            mavenLocal()
        }
        dependencies {
            classpath group: 'org.intermine', name: 'plugin', version: '1.0-SNAPSHOT'
        }
    }

subprojects {
    group = 'org.intermine'
    version = '2.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'database'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        mavenLocal()
    }

    configurations {
        bioModel
        mergeModel
    }
    
    dependencies {
        compile group: 'log4j', name: 'log4j', version: '1.2.17'
        compile group: 'org.intermine', name: 'bio-core', version: bioVersion
        compile group: 'org.intermine', name: 'intermine-model', version: imVersion
        compile group: 'org.intermine', name: 'intermine-integrate', version: imVersion
        compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
        testCompile group: 'junit', name: 'junit', version: '4.8.2'
        testCompile group: "org.intermine", name: "intermine-integrate", version: imVersion, classifier: "testClasses"
        testCompile group: 'xmlunit', name: 'xmlunit', version: '1.0'
        bioModel group: 'org.intermine', name: 'bio-model', version: bioVersion, transitive: false 
        mergeModel group : "org.intermine", name: "intermine-objectstore", version: imVersion
    }

    dbConfig {
        objectStoreName = "os.bio-test"
        modelName = "genomic"
        defaultInterminePropertiesFile = "default.intermine.test.properties"
    }

    task copyModelProperties(type: Copy) {
        from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
        into sourceSets.main.output.resourcesDir

        rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }

        inputs.sourceFiles.stopExecutionIfEmpty()
    }

    task copyBioGenomicModel(type: Copy) {
        dependsOn 'processResources'
        description ""
        from({ zipTree(configurations.bioModel.singleFile) }) {
            include "genomic_model.xml"
        }
        into sourceSets.main.output.resourcesDir
    }

    task mergeModels {
        dependsOn 'copyBioGenomicModel', 'copyModelProperties'

        doLast {
            def bioSourceAdditionsFile = project.name.minus("bio-source-") + "_additions.xml";
            if ((new File("$sourceSets.main.output.resourcesDir/" + bioSourceAdditionsFile)).exists()) {
                ant.taskdef(name: "mergeBioSourceModel", classname: "org.intermine.task.ModelMergerTask") {
                    classpath {
                        pathelement(path: configurations.mergeModel.asPath)
                        dirset(dir: buildDir)
                    }
                }          
                ant.mergeBioSourceModel(inputModelFile: "$sourceSets.main.output.resourcesDir/genomic_model.xml", additionsFile: bioSourceAdditionsFile,
                    outputFile: "$sourceSets.main.output.resourcesDir/genomic_model.xml")
            }
        }
    }
    
    jar {exclude "intermine.properties"}

    //test configuration

    //only buildDB if there are tests to run
    def srcDir = "$project.projectDir" + "/src/test"
    if ((new File(srcDir)).exists()) {
        tasks.test.dependsOn 'buildDB'
    }

    tasks.withType(Test) {
        scanForTestClasses = false
        include '**/*Test.class'
        test.reports.html.enabled = false
    }

    // Disable the test report for the individual test task
    test {
        ignoreFailures = true
        test.reports.html.enabled = false
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
