sourceSets {
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

dependencies {
    testCompile project(':intermine-objectstore')
    testCompile 'commons-lang:commons-lang:2.6'
}

configurations {
    testBioArtifacts
}

void mergeSourceModels(model, extraModel) {
    ant.taskdef(name: 'mergeSourceModelsTask', classname: 'org.intermine.task.MergeSourceModelsTask') {
      classpath {
        fileset(dir: '../../intermine/model/build/libs/', includes: '*.jar')
        dirset(dir: '../../intermine/api/src/test/resources/')
        dirset(dir: '../core/')
        pathelement(path: configurations.testCompile.asPath)
      }   
    }   
    ant.mergeSourceModelsTask(modelFile: model, extraModelPathsStart: extraModel, baseDir: '../..')
}

task mergeBioSourceModel {
    doLast {
        mergeSourceModels('genomic', 'bio/core/genomic_additions.xml')
    }   
}

void generateModel(model, destination, type) {
    ant.taskdef(name: 'modelOutputTask', classname: 'org.intermine.task.ModelOutputTask') {
      classpath {
        fileset(dir: '../../intermine/model/build/libs/', includes: '*.jar')
        dirset(dir: '../../intermine/api/src/test/resources/')
        pathelement(path: configurations.testCompile.asPath)
      }   
    }   
    ant.modelOutputTask(model: model, destDir: destination, type: type)
}

task generateBioModel {
    doLast {
        generateModel('genomic', 'src/test/java', 'java')
    }   
}

task copyBioTestProperties(type: Copy) {
    from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
    into sourceSets.test.output.resourcesDir

    rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }

    inputs.sourceFiles.stopExecutionIfEmpty()
}

task buildBioModelJar(type: Jar) {
    from(sourceSets.test.output)
}

tasks.compileTestJava.dependsOn 'generateBioModel'
tasks.generateBioModel.dependsOn 'mergeBioSourceModel'
tasks.buildBioModelJar.dependsOn 'copyBioTestProperties', 'testClasses'

artifacts {
    testBioArtifacts buildBioModelJar
}
