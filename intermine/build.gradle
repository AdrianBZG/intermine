buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.researchgate:gradle-release:2.6.0"
    }
}

subprojects {
    group = 'org.intermine'
    apply plugin: 'net.researchgate.release'
    apply plugin: "java"
    apply plugin: "maven"

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        mavenLocal()
    }

    release {
        failOnCommitNeeded = true
        failOnPublishNeeded = true
        failOnSnapshotDependencies = true
        failOnUnversionedFiles = true
        failOnUpdateNeeded = true
        revertOnFail = true
        preCommitText = ''
        preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
        tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
        newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
        version =  "2.0.0"
        tagTemplate = '${version}'
        versionPropertyFile = 'gradle.properties'
        versionProperties = []
        buildTasks = ['build']
//    versionPatterns = [
//            /(\d+)([^\d]*$)/: { Matcher m, Project p -> m.replaceAll("${(m[0][1] as int) + 1}${m[0][2]}") }
//    ]

        git {
            requireBranch = 'gradle'
            pushToRemote = 'origin'
            pushToBranchPrefix = ''
            commitVersionFileOnly = false
            signTag = false
        }
    }


    if (it.name != 'intermine-resources') {
        dependencies {
            compile group: 'ant', name: 'ant', version: '1.6.5'
            compile group: 'log4j', name: 'log4j', version: '1.2.17'
            compile group: 'antlr', name:'antlr', version: '2.7.7'
            testCompile group: 'junit', name: 'junit', version: '4.8.2'
        }
    }

    tasks.withType(Test) {
        scanForTestClasses = false
        include '**/*Test.class'
        test.reports.html.enabled = false
    }

    test {
        ignoreFailures = true
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

ext {
    packageLicenses = ['LGPL-2.1 ']
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

