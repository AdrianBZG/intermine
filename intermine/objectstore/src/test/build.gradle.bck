ant.importBuild('build.xml') {
    antTargetName -> 'ost-' + antTargetName
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources', 'build/main']
            include '*.properties'
        }
    }
    test {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
}

dependencies {
  compile project(':testmodel')
  compile project(':intermine-integrate-main')
  compile fileTree(dir: "$projectDir/lib", include: '*.jar') //org.gnu.readline is not in a public repo
  compile group: 'junit', name: 'junit', version: '4.8.1'
  compile group: 'mockobjects', name: 'mockobjects-core', version: '0.09'
  compile group: 'mockobjects', name: 'mockobjects-jdk1.3', version: '0.09'
  compile group: 'antlr', name: 'antlr', version: '2.7.6'
}

task setClassLoader {
  doLast {
    ClassLoader antClassLoader = org.apache.tools.ant.Project.class.classLoader
      configurations.runtime.each { File f ->
        antClassLoader.addURL(f.toURI().toURL())
    }
    antClassLoader.addURL(file('./build/main/').toURI().toURL()) //default.intermine.properties and intermine.properties in the classpath
  }
}


def buildDB(String osname, String model) {
    ant.taskdef(name: 'buildDBUnittest', classname: 'org.intermine.task.BuildDbTask')
    ant.buildDBUnittest(osname: "$osname", model: "$model", schemafile:"${osname}-schema.xml", tempDir: 'build/tmp')
}

task buildObjectStoreTestDBs {
    doLast {
      buildDB('os.unittest', 'testmodel')
      buildDB('os.truncunittest', 'testmodel') 
    }    
}


//default.intermine.properties and intermine.properties
//tasks.test.dependsOn 'ost--init-properties'
//tasks.compileJava.mustRunAfter 'ost--init-properties'

tasks.test.dependsOn 'buildObjectStoreTestDBs'
tasks.test.mustRunAfter 'buildObjectStoreTestDBs'

tasks.buildObjectStoreTestDBs.dependsOn 'ost--init-properties'
tasks.buildObjectStoreTestDBs.mustRunAfter 'ost--init-properties'
tasks.buildObjectStoreTestDBs.dependsOn 'setClassLoader'
tasks.buildObjectStoreTestDBs.mustRunAfter 'setClassLoader'



