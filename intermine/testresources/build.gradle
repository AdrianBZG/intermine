// Prevent the build target from building an empty compile jar
// we only have testCompile classes and we want them to be in the jar instead via the buildTestModelJar target
jar.enabled = false

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

dependencies {
    testCompile project(':intermine-objectstore')
    testCompile 'commons-lang:commons-lang:2.6'
}

configurations {
    testArtifacts
}

void generateModel(model, destination, type) {
    ant.taskdef(name: 'modelOutputTask', classname: 'org.intermine.task.ModelOutputTask') {
        classpath {
            fileset(dir: '../model/build/libs/', includes: '*.jar')
            dirset(dir: 'src/test/resources/')
            pathelement(path: configurations.testCompile.asPath)
        }
    }   

    ant.modelOutputTask(model: model, destDir: destination, type: type)
}

task copyTestProperties(type: Copy) {
    from "../../config/intermine-test.properties"
    into sourceSets.test.output.resourcesDir

    rename { fileName -> fileName.replace('intermine-test.properties', 'intermine.properties') }

    inputs.sourceFiles.stopExecutionIfEmpty()
}

task generateTestModel {
    doLast {
        generateModel('testmodel', 'src/test/java', 'java')
    }   
}

task buildTestModelJar(type: Jar) {
    from(sourceSets.test.output)
}

tasks.compileTestJava.dependsOn 'generateTestModel'
tasks.buildTestModelJar.dependsOn 'copyTestProperties', 'testClasses'

artifacts {
    testArtifacts buildTestModelJar
}
